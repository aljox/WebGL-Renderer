precision mediump float;

struct Light {
    vec3 s_lightColour/*light*/;
    float s_ambient/*light*/;
    float s_diffuse/*light*/;
    float s_specular/*light*/;
};

uniform Light light;
uniform sampler2D u_sampler0;

varying vec2 v_textureCord;
varying vec3 v_normal;
varying vec3 v_lightPos;
varying vec3 v_fragPosition;

void main() {
  vec3 objectColour = vec3(texture2D(u_sampler0, v_textureCord));

  // ambient
  vec3 ambient =  light.s_ambient * objectColour;

  // diffuse
  vec3 norm = normalize(v_normal);
  vec3 incident = normalize(v_lightPos - v_fragPosition);
  float diff = max(dot(incident, norm), 0.0);
  vec3 diffuse = light.s_diffuse * (objectColour * diff) * light.s_lightColour;

  // specular
  vec3 viewDir = normalize(-v_fragPosition);
  vec3 reflectDir = reflect(-incident, norm);
  float spec = max(dot(viewDir, reflectDir), 0.0);
  spec = pow(spec, 64.0);
  vec3 specular = light.s_specular * (objectColour * spec) * light.s_lightColour;

  vec3 result = ambient + diffuse + specular;
  gl_FragColor = vec4(result, 1.0);
}
